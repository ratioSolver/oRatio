cmake_minimum_required(VERSION 3.5.0)
project(oRatioLib VERSION 0.2.0 LANGUAGES CXX)
set(CMAKE_CXX_FLAGS_DEBUG "${CMAKE_CXX_FLAGS_DEBUG} -fsanitize=undefined")

include(CTest)
enable_testing()

set(JSON_INCLUDE_UTILS OFF CACHE BOOL "Enable JSON Utils" FORCE)

add_subdirectory(extern/riddle)
add_subdirectory(extern/semitone)

set(TEMPORAL_NETWORK_TYPES LRA DL)
set(TEMPORAL_NETWORK_TYPE LRA CACHE STRING "Temporal network type")
set_property(CACHE TEMPORAL_NETWORK_TYPE PROPERTY STRINGS ${TEMPORAL_NETWORK_TYPES})
list(FIND TEMPORAL_NETWORK_TYPES ${TEMPORAL_NETWORK_TYPE} TEMPORAL_NETWORK_TYPE_INDEX)
if(${TEMPORAL_NETWORK_TYPE_INDEX} EQUAL -1)
    message(FATAL_ERROR "Invalid temporal network type ${TEMPORAL_NETWORK_TYPE}")
endif()
message(STATUS "Temporal network type: ${TEMPORAL_NETWORK_TYPE}")

set(HEURISTIC_TYPES h_max h_add h2_max h2_add)
set(HEURISTIC_TYPE h_max CACHE STRING "Heuristic type")
set_property(CACHE HEURISTIC_TYPE PROPERTY STRINGS ${HEURISTIC_TYPES})
list(FIND HEURISTIC_TYPES ${HEURISTIC_TYPE} HEURISTIC_TYPE_INDEX)
if(${HEURISTIC_TYPE_INDEX} EQUAL -1)
    message(FATAL_ERROR "Invalid heuristic type ${HEURISTIC_TYPE}")
endif()
message(STATUS "Heuristic type: ${HEURISTIC_TYPE}")

add_library(oRatioLib src/solver.cpp src/solver_item.cpp src/graph.cpp src/flaw.cpp src/resolver.cpp src/smart_type.cpp src/flaws/atom_flaw.cpp src/flaws/bool_flaw.cpp src/flaws/disj_flaw.cpp src/flaws/disjunction_flaw.cpp src/flaws/enum_flaw.cpp)
target_link_libraries(oRatioLib PUBLIC RiDDLe SeMiTONE)
target_include_directories(oRatioLib PUBLIC $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include> $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include/flaws> $<BUILD_INTERFACE:${CMAKE_CURRENT_BINARY_DIR}>)

if(${TEMPORAL_NETWORK_TYPE} STREQUAL "LRA")
    set(INIT_STRING "predicate Impulse(real at) { at >= origin; at <= horizon; } predicate Interval(real start, real end) { start >= origin; start <= end; end <= horizon; } real origin, horizon; origin >= 0.0; origin <= horizon;" CACHE STRING "Initialization string")
    target_compile_definitions(oRatioLib PRIVATE LRA_TN)
elseif(${TEMPORAL_NETWORK_TYPE} STREQUAL "DL")
    set(INIT_STRING "predicate Impulse(time at) { at >= origin; at <= horizon; } predicate Interval(time start, time end) { start >= origin; start <= end; end <= horizon; } time origin, horizon; origin >= 0.0; origin <= horizon;" CACHE STRING "Initialization string")
    target_compile_definitions(oRatioLib PRIVATE DL_TN)
endif()
configure_file(include/init.hpp.in init.hpp @ONLY)

if(${HEURISTIC_TYPE} STREQUAL "h_max")
    target_compile_definitions(oRatioLib PRIVATE H_MAX)
elseif(${HEURISTIC_TYPE} STREQUAL "h_add")
    target_compile_definitions(oRatioLib PRIVATE H_ADD)
elseif(${HEURISTIC_TYPE} STREQUAL "h2_max")
    target_compile_definitions(oRatioLib PRIVATE H2_MAX)
elseif(${HEURISTIC_TYPE} STREQUAL "h2_add")
    target_compile_definitions(oRatioLib PRIVATE H2_ADD)
endif()

add_executable(oRatio src/main.cpp)
target_link_libraries(oRatio PRIVATE oRatioLib)
target_include_directories(oRatio PRIVATE $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>)

if(BUILD_TESTING)
    add_subdirectory(tests)
endif()

set(CPACK_PROJECT_NAME ${PROJECT_NAME})
set(CPACK_PROJECT_VERSION ${PROJECT_VERSION})
include(CPack)
